apply plugin: 'java'
apply plugin: 'eclipse'

sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6

eclipse {
    classpath {
        downloadSources = true
        downloadJavadoc = true
    }
}

dependencies {
    compile project(':yab-core')
    compile 'io.airlift:airline:0.5'
    testCompile 'org.testng:testng:6.8.5'
}

// create a self-contained jar that is executable
// the output is both a 'fat' project artifact and
// a convenience file named "build/yab"

task fatJar(dependsOn: classes, type: Jar) {
    classifier 'fat'

    doFirst {
        // Delay evaluation until the compile configuration is ready
        from {
            configurations.compile.collect { zipTree(it).matching { include "**/*" } }
        }
    }

    from(sourceSets*.output.classesDir) {
        include "**/*"
    }

    // really executable jar
    // http://skife.org/java/unix/2011/06/20/really_executable_jars.html

    manifest {
        attributes 'Main-Class': 'com.axemblr.yab.cli.YaB'
        attributes("Implementation-Title": "YaB", "Specification-Version": version, "Implementation-Version": version)
    }

    // for convenience, we make a file in the build dir named yab with no extension
    doLast {
        def srcFile = new File("${buildDir}/libs/${archiveName}")

        def shortcutFile = new File("${buildDir}/yab")
        shortcutFile.delete()
        shortcutFile << "#!/usr/bin/env sh\n"
        shortcutFile << 'exec java -jar $0 "$@"' + "\n"
        shortcutFile << srcFile.bytes
        shortcutFile.setExecutable(true, true)
    }
}

artifacts {
    archives fatJar
}